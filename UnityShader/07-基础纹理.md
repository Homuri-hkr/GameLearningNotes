### 7.1 单张纹理

#### 7.1.1 实践

```c++
Shader "Unity Shaders Book/Chapter 7/Single Texture"{
	Properties{
		_Color("Color Tint", Color) = (1, 1, 1, 1)
		_MainTex("Main Tex", 2D) = "white" { }
		_Specular("Specular", Color) = (1, 1, 1, 1)
		_Gloss("Gloss", Range(8.0, 256)) = 20
	}

	SubShader{
		pass{
			Tags { "LightMode" = "ForwardBase"}

			CGPROGRAM
			
			#pragma vertex vert
			#pragma fragment frag

			#include "Lighting.cginc"

			fixed4 _Color;
			sampler2D _MainTex;
			//使用纹理名_ST声明某个纹理的属性，S是缩放T是平移
			float4 _MainTex_ST;
			fixed4 _Specular;
			float _Gloss;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float4 texcoord : TEXCOORD0;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
				float2 uv : TEXCOORD2;
			};

			v2f vert(a2v v){
				v2f o;
				o.pos = UnityObjectToClipPos(v.vertex);
				o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
				//_MainTex_ST.xy是缩放属性，_MainTex_ST.zw是偏移属性
				//可以使用TRANSFORM_TEX(v.texcoord, _MainTex)函数进行替代，在UnityCG.cginc中定义
				o.uv = v.texcoord.xy * _MainTex_ST.xy + _MainTex_ST.zw;

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));

				fixed3 albedo = tex2D(_MainTex, i.uv).rgb * _Color.rgb;

				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
				fixed3 diffuse = _LightColor0.rgb * albedo * saturate(dot(i.worldNormal, worldLightDir));
                
				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
				fixed3 halfDir = normalize(worldLightDir + viewDir);
				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(i.worldNormal, halfDir)), _Gloss);

				fixed3 color = ambient + diffuse + specular;
				return fixed4(color, 1.0);
			}
			ENDCG
		}
	}
	FallBack "Specular"
}
```

#### 7.1.2 纹理的属性

- 图片在unity Inspector面板上的属性：
  - Texture Type：图片类型，让Unity知道用户的意图，为Unity Shader传递正确的纹理，并在一些情况下对其进行优化
  - Alpha Source：透明通道的值的来源
  - Warp Mode：决定当纹理坐标超过范围后如何平铺
  - Filter Mode：纹理由纹理变换而产生拉伸时将会采用哪种滤波模式，会影响放大或缩小纹理时得到的图片。
    1. Point：使用了最近邻滤波，在放大缩小中，它的采样像素数目通常只有一个。因此看上去有种像素风格
    2. Bilinear：线性滤波，对于每个目标像素找到4个临近像素，然后进行  插值混合得到最终像素，因此图像看上去被模糊了
    3. Trilinear
    
    三种方式，滤波效果依次提升，但需要耗费的性能也依次增大
  - MaxSize：最大分辨率，若导入纹理的大小超过了该值，则Unity会将纹理缩放为最大分辨率
  - Format：决定Unity内部使用哪种格式存储该纹理
- 纹理缩小的过程比放大更加复杂一些， 此时原纹理中的多个纹理中的多个像素将会对应一个目标像素，往往需要处理抗锯齿问题。一个最常使用的方式是使用多级渐远纹理（mipmapping）技术，原理是先用滤波处理来得到很多更小的图像，每一层都是上一层图像降采样的结果。Unity中在Advanced中勾选Generate Mipmap开启多级渐远纹理

### 7.2 凹凸映射

- 凹凸映射使用一张纹理来修改模型表面的法线，以便提供更多细节。该方法不会真正改变模型的顶点位置
- 有两种方法可以用来进行凹凸映射：
  1. 高度映射：使用高度纹理，模拟表面位移，然后得到一个修改后的法线值
  2. 法线映射：使用法线纹理直接存储表面法线

#### 7.2.1 高度纹理

- 高度图中存储的是强度值，用于表示模型局部的海拔高度，所以颜色越浅表明该位置越向外凸。该方法的好处是可以明确知道一个模型表面的凹凸情况，缺点是计算更复杂，不能直接得到表面法线

#### 7.2.2 法线纹理

- 法线纹理中存储的就是表面的法线方向，法线分量范围在[-1, 1]，而像素的分量范围为[0, 1]，所以要进行映射

- 法线纹理中存储的法线方向使用的坐标空间：

  1. 模型空间，将修改后的模型空间中的表面法线存储在一个纹理中，称为模型空间的法线纹理
  2. 模型顶点的切线空间，对于模型的每个顶点都有一个属于自己的切线空间，原点就是该顶点本身，z轴是顶点的法线方向，x轴是顶点的切线方向，y轴由法线和切线的叉积得到，称为副切线或副法线。称为切线空间的法线纹理

- 模型空间下的法线纹理看起来是五颜六色的，因为所有法线在同一个坐标空间，每个点存储的法线方向是各异的。

  切线空间下的法线纹理看上去几乎是浅蓝色的，每个法线方向所在的坐标空间不一样，这种法线纹理存储的每个点在各自的切线空间中的法线扰动方向，若一个点法线方向不变，即值为（0，0，1），映射后为（0.5，0.5，1）为浅蓝色

- 使用模型空间来存储法线的优点：

  - 实现简单，更加直观。甚至不需要模型原始的法线和切线等信息，计算更少，生成也非常简单。若要生成切线空间下的法线纹理，由于模型的切线一般是和UV方向相同，因此想要得到效果比较好的法线映射就要求纹理映射是连续的
  - 在纹理坐标的缝合处和尖锐的边角部分，可见的突变较少，可以提供平滑的边界。因为模型空间下的法线纹理存储的是同一坐标系下的法线信息，因此在边界出通过插值得到的法线可以平滑变换。而切线空间下的法线纹理中的法线信息是依靠纹理坐标的方向得到的结果，可能在边缘处或尖锐部分造成更多可见的缝合迹象

- 使用切线空间存储法线的优点：

  - 自由度很高。模型空间下的法线纹理记录的是绝对法线信息，仅可用于创建它时的那个模型，而应用到其他模型上的效果就完全错误了。切线空间下的法线纹理记录的是相对法线信息，应用到不同的网格上也可以得到一个合理的结果
  - 可进行UV动画。例如可以移动一个UV坐标实现一个凹凸移动的效果
  - 可以重用法线纹理，如对于砖块只需一个法线纹理就可以运用到6个面上
  - 可压缩。由于切线空间下的法线纹理中的Z方向总是正方向，所以可以仅存储XY方向推导得Z方向。若模型空间下的法线纹理由于各个方向都是可能的，所以必须存储三个方向的值

#### 7.2.3 实践

- 由于法线纹理中存储的法线是切线空间下的方向，通常有两种选择选择计算光照：

  1. 在切线空间下进行光照计算，需要将光照方向、视角方向变换到切线空间下
  2. 在世界空间下进行光照计算，将采样得到的法线方向变换到世界空间下

  从效率上，第一种方法优于第二种方法，因为可以在顶点着色器中就完成对光照方向和视角方向的变换，而第二种需要对法线纹理进行采样，变换过程必须在片元着色器

  从通用性上，第二种方式优于第一种，因为有些需要在世界空间下进行一些计算

- 在切线空间计算

  ```c++
  Shader "Unity Shaders Book/Chapter 7/Normal Map In Tangent Space"{
  	Properties{
  		_Color ("Color Tint", Color) = (1, 1, 1, 1)
  		_MainTex ("Main Tex", 2D) = "white" {}
  		//bump是unity内置的法线纹理，当没提供任何法线纹理时，对应了模型自带的法线信息
  		_BumpMap ("Normal Map", 2D) = "bump" {}
  		//控
          
  		_BumpScale ("Bump Scale", Float) = 1.0
  		_Specular ("Specular", Color) = (1, 1, 1, 1)
  		_Gloss ("Gloss", Range(8.0, 256)) = 20
  	}
  
  	SubShader{
  		Pass{
  			Tags { "LightMode" = "ForwardBase" }
  
  			CGPROGRAM
  
  			#pragma vertex vert 
  			#pragma fragment frag
  
  			#include "Lighting.cginc"
  
  			fixed4 _Color;
  			sampler2D _MainTex;
  			float4 _MainTex_ST;
  			sampler2D _BumpMap;
  			float4 _BumpMap_ST;
  			float _BumpScale;
  			fixed4 _Specular;
  			float _Gloss;
  
  			struct a2v{
  				float4 vertex : POSITION;
  				float3 normal : NORMAL;
  				float4 tangent : TANGENT;
  				float4 texcoord : TEXCOORD0;
  			};
  
  			struct v2f{
  				float4 pos : SV_POSITION;
  				float4 uv : TEXCOORD0;
  				float3 lightDir : TEXCOORD1;
  				float3 viewDir : TEXCOORD2;
  			};
  
  			v2f vert(a2v v){
  				v2f o;
  				o.pos = UnityObjectToClipPos(v.vertex);
  
  				o.uv.xy = v.texcoord.xy * _MainTex_ST.xy + _MainTex_ST.zw;
  				o.uv.zw = v.texcoord.xy * _BumpMap_ST.xy + _BumpMap_ST.zw;
  				//求副切线
  				float3 binormal = cross(normalize(v.normal), normalize(v.tangent.xyz)) * v.tangent.w;
  				//以切线为x轴，副切线y轴，法线z轴构建切线空间到模型空间的转换矩阵。当一个变换仅存在平移和旋转，则该变换的逆矩阵等于转置矩阵
  				//所以模型空间到切线空间的逆矩阵等于切线空间到模型空间的矩阵
  				float3x3 rotation = float3x3(v.tangent.xyz, binormal, v.normal);
  				//float3x3 rotation = TANGENT_SPACE_ROTATION;
  				
                  // 先转到模型空间，再转到物体的切线空间
  				o.lightDir = normalize(mul(rotation, ObjSpaceLightDir(v.vertex)).xyz);
  				o.viewDir = normalize(mul(rotation, ObjSpaceViewDir(v.vertex)).xyz);
  
  				return o;
  			}
  
  			fixed4 frag(v2f i) : SV_Target{
  				fixed4 packedNormal = tex2D(_BumpMap, i.uv.zw);
  				fixed3 tangentNormal;
  				//若纹理没有被标记为"Normal Map"
  				//tangentNormal.xy = (packedNormal.xy * 2 - 1) * _BumpScale;
  				//tangentNormal.z = sqrt(1.0 - saturate(dot(tangentNormal.xy, tangentNormal.xy)));
  				//若标记为"Normal Map"，使用内置函数
  				tangentNormal = UnpackNormal(packedNormal);
  				tangentNormal.xy *= _BumpScale;
  				tangentNormal.z = sqrt(1.0 - saturate(dot(tangentNormal.xy, tangentNormal.xy)));
  
  				fixed3 albedo = tex2D(_MainTex, i.uv).rgb * _Color.rgb;
  				fixed ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
  
  				fixed3 diffuse = _LightColor0.rgb * albedo * saturate(dot(tangentNormal, i.lightDir));
  
  				fixed3 halfDir = normalize(i.lightDir + i.viewDir);
  				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(tangentNormal, halfDir)), _Gloss);
  
  				fixed3 color = ambient + diffuse + specular;
  				return fixed4(color, 1.0);
  			}
  
  			ENDCG
  		}
  	}
  	FallBack "Specular"
  }
  ```

- 在世界空间计算

  ```c++
  Shader "Unity Shaders Book/Chapter 7/Normal Map In World Space"{
  	
  	Properties{
  		_Color("Color Tint", Color) = (1, 1, 1, 1)
  		_MainTex("Main Tex", 2D) = "white" {}
  		_BumpMap("Normal Map", 2D) = "bump" {}
  		_BumpScale("Bump Scale", Float) = 1.0
  		_Specular("Specular", Color) = (1, 1, 1, 1)
  		_Gloss("Gloss", Range(8.0, 256)) = 20
  	}
  
  	SubShader{
  		pass{
  			Tags {"LightMode" = "ForwardBase"}
  			
  			CGPROGRAM
  			
  			#pragma vertex vert 
  			#pragma fragment frag 
  
  			#include "Lighting.cginc"
  
  			fixed4 _Color;
  			sampler2D _MainTex;
  			float4 _MainTex_ST;
  			sampler2D _BumpMap;
  			float4 _BumpMap_ST;
  			float _BumpScale;
  			fixed4 _Specular;
  			float _Gloss;
  
  			struct a2v{
  				float4 vertex : POSITION;
  				float3 normal : NORMAL;
  				float4 tangent : TANGENT;
  				float4 texcoord : TEXCOORD0;
  			};
  
  			struct v2f{
  				float4 pos : SV_POSITION;
  				float4 uv : TEXCOORD0;
  				float4 TtoW0 : TEXCOORD1;
  				float4 TtoW1 : TEXCOORD2;
  				float4 TtoW2 : TEXCOORD3;
  			};
  
  			v2f vert(a2v v){
  				v2f o;
  				o.pos = UnityObjectToClipPos(v.vertex);
  
  				o.uv.xy = v.texcoord.xy * _MainTex_ST.xy + _MainTex_ST.zw;
  				o.uv.zw = v.texcoord.xy * _BumpMap_ST.xy + _BumpMap_ST.zw;
  
  				float3 worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
  				fixed3 worldNormal = UnityObjectToWorldNormal(v.normal);
  				fixed3 worldTangent = UnityObjectToWorldDir(v.tangent.xyz);
  				fixed3 worldBinormal = cross(worldNormal, worldTangent) * v.tangent.w;
  
  				//将x，y，z分量存切线空间到世界空间变换矩阵的每行，w分量存世界空间下的顶点位置以充分利用插值寄存器的存储空间
  				o.TtoW0 = float4(worldTangent.x, worldBinormal.x, worldNormal.x, worldPos.x);
  				o.TtoW1 = float4(worldTangent.y, worldBinormal.y, worldNormal.y, worldPos.y);
  				o.TtoW2 = float4(worldTangent.z, worldBinormal.z, worldNormal.z, worldPos.z);
  
  				return o;
  			}
  
  			fixed4 frag(v2f i) : SV_Target{
  				float3 worldPos = float3(i.TtoW0.w, i.TtoW1.w, i.TtoW2.w);
  
  				fixed3 lightDir = normalize(UnityWorldSpaceLightDir(worldPos));
  				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(worldPos));
  
  				fixed3 bump = UnpackNormal(tex2D(_BumpMap, i.uv.zw));
  				bump.xy *= _BumpScale;
  				bump.z = sqrt(1.0 - saturate(dot(bump.xy, bump.xy)));
  				
  				bump = normalize(half3(dot(i.TtoW0.xyz, bump), dot(i.TtoW1.xyz, bump), dot(i.TtoW2.xyz, bump)));
  			
  				fixed3 albedo = tex2D(_MainTex, i.uv).rgb * _Color.rgb;
  				fixed ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
  
  				fixed3 diffuse = _LightColor0.rgb * albedo * saturate(dot(bump, lightDir));
  
  				fixed3 halfDir = normalize(lightDir + viewDir);
  				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(bump, halfDir)), _Gloss);
  
  				fixed3 color = ambient + diffuse + specular;
  				return fixed4(color, 1.0);
  			}
  
  			ENDCG
  		}
  	}
  	FallBack "Specular"
  }
  ```

#### 7.2.4 Unity中的法线纹理类型

- 将法线纹理图片的Texture Type设为Normal Type可以让Unity根据不同平台对纹理进行压缩，然后通过UnpackNormal来针对不同的压缩格式对法线纹理进行正确采样

- 当选择Texture Type为Normal Type后，会出现一个复选框Create from Grayscale，勾选表示从高度图生产法线纹理，通过勾选后多出两个选项：

  Bumpiness控制凹凸程度

  Filtering决定使用什么方式计算凹凸程度：

  1. Smooth，使生成后的法线纹理比较平滑
  2. Sharp，使用Sobel滤波生成法线（一种边缘检测时使用的滤波器），实现方式是对于高度图中的每个像素，考虑水平方向和数值方向的像素差，把差当成该点对应的法线在x和y方向的位移，然后使用映射函数存储到法线纹理的r和g分量

### 7.3 渐变纹理

- 纹理可以用于存储任何表面属性
- 可以使用渐变纹理来控制漫反射光照的结果，不同的渐变纹理有不同的特性
- 一种基于冷到暖色调的着色技术，用来得到一种插画风格的渲染效果，可以保证物体的轮廓线相比之前使用的传统漫反射光照更加明显，且能够提供多种色调变换。现在很多卡通风格的渲染都使用了该技术
- 渐变纹理的Warp Mode应设为Clamp模式，防止对纹理进行采样时由于浮点数精度造成问题，理论上对渐变纹理进行采样时的值在[0, 1]但仍可能超过该范围导致采样到另一个边界造成有的地方会起小斑点

```c++
Shader "Unity Shaders Book/Chapter 7/Ramp Texture"{
	Properties{
		_Color ("Color Tint", Color) = (1, 1, 1, 1)
		_RampTex("Ramp Tex", 2D) = "white" {}
		_Specular("Specular", Color) = (1, 1, 1, 1)
		_Gloss("Gloss", Range(8.0, 256)) = 20
	}

	SubShader{
		pass{
			Tags { "LightMode" = "ForwardBase" }

			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag

			#include "Lighting.cginc"

			fixed4 _Color;
			sampler2D _RampTex;
			float4 _RampTex_ST;
			fixed4 _Specular;
			float _Gloss;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float4 texcoord : TEXCOORD0;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
				float2 uv : TEXCOORD2;
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
				o.uv = TRANSFORM_TEX(v.texcoord, _RampTex);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
				
				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;

				fixed halfLambert = 0.5 * dot(i.worldNormal, worldLightDir) + 0.5;
				//由于渐变纹理实际上是一维纹理，在纵轴上的颜色不变，所有可以只需要指定纹理的横坐标即可确定
				//这里使用半兰伯特作为纹理坐标进行采样，和光夹角大的在取渐变纹理左边的值，大的取右边的值
				fixed3 diffuseColor = tex2D(_RampTex, fixed2(halfLambert, halfLambert)).rgb * _Color.rgb;
				fixed3 diffuse = _LightColor0.rgb * diffuseColor;

				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
				fixed3 halfDir = normalize(worldLightDir + viewDir);
				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(i.worldNormal, halfDir)), _Gloss);

				fixed3 color = ambient + diffuse + specular;

				return fixed4(color, 1.0);
			}
			ENDCG
		}
	}
	FallBack "Specular"
}
```

### 7.4 遮罩纹理

- 遮罩允许保护某些区域，使其免于某些修改
- 使用遮罩纹理的一般流程是：通过采样得到遮罩纹理的纹素值，然后使用其中某个或某几个通道的值，与某种表面属性相乘，当该通道的值为0时，可以保护表面不受该属性的影响，可以让美术人员更加精准的控制模型表面的各种性质

#### 7.4.1 实践

```c++
// Upgrade NOTE: replaced 'mul(UNITY_MATRIX_MVP,*)' with 'UnityObjectToClipPos(*)'

Shader "Unity Shaders Book/Chapter 7/Mask Texture"{
	Properties{
		_Color ("Color Tint", Color) = (1, 1, 1, 1)
		_MainTex("Main Tex", 2D) = "white" {}
		_BumpMap("Normal Map", 2D) = "bump" {}
		_BumpScale("Bump Scale", Float) = 1.0
		_SpecularMask("Specular Mask", 2D) = "white" {}
		_SpecularScale("Specular Scale", Float) = 1.0
		_Specular("Specular", Color) = (1, 1, 1, 1)
		_Gloss("Gloss", Range(8.0, 256)) = 20
	}

	SubShader{
		pass{
			Tags { "LightMode" = "ForwardBase" }

			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag 

			#include "Lighting.cginc"

			//很多时候并不需要为所有纹理都定义一个位移和平铺操作，因为多数情况可以共用
			fixed4 _Color;
			sampler2D _MainTex;
			float4 _MainTex_ST;
			sampler2D _BumpMap;
			float _BumpScale;
			sampler2D _SpecularMask;
			float _SpecularScale;
			fixed4 _Specular;
			float _Gloss;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float4 tangent : TANGENT;
				float4 texcoord : TEXCOORD0;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float2 uv : TEXCOORD0;
				float3 lightDir : TEXCOORD1;
				float3 viewDir : TEXCOORD2;
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.uv.xy = v.texcoord.xy * _MainTex_ST.xy + _MainTex_ST.zw;

				TANGENT_SPACE_ROTATION;
				o.lightDir = normalize(mul(rotation, ObjSpaceLightDir(v.vertex)).xyz);
				o.viewDir = normalize(mul(rotation, ObjSpaceViewDir(v.vertex)).xyz);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				fixed3 tangentNormal = UnpackNormal(tex2D(_BumpMap, i.uv));
				tangentNormal.xy *= _BumpScale;
				tangentNormal.z = sqrt(1.0 - saturate(dot(tangentNormal.xy, tangentNormal.xy)));

				fixed3 albedo = tex2D(_MainTex, i.uv).rgb * _Color.rgb;

				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;

				fixed3 diffuse = _LightColor0.rgb * albedo * saturate(dot(tangentNormal, i.viewDir));

				fixed3 halfDir = normalize(i.lightDir + i.viewDir);
				fixed specularMask = tex2D(_SpecularMask, i.uv).r * _SpecularScale;
				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(tangentNormal, halfDir)), _Gloss) * specularMask;

				fixed3 color = ambient + diffuse + specular;
				return fixed4(color, 1.0);
			}
			ENDCG
		}
	}
	FallBack "Specular"
}
```

####  7.4.2 其他遮罩纹理

- 在真实的游戏制作过程中，遮罩纹理不仅限于保护某些区域免于被修改，而可以存储任何希望逐像素控制的表面属性。通常会充分利用纹理的RGBA四个通道用于存储不同的属性
