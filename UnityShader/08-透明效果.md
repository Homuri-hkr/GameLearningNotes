- 透明度测试：只要一个片元的透明度不满足条件，其对应的片元就会被丢弃，被舍弃的片元将不会进行任何处理，也不会对颜色缓冲产生影响。否则就会按普通的不透明物体的处理方式进行处理，可以进行深度测试和深度写入
- 透明度混合：使用当前片元的透明度作为混合因子，与已经存储在颜色缓冲中的颜色值进行混合，得到新颜色。但透明度混合要关闭深度写入，否则只能看见透明的物体，无法通过透明物体看到透明物体之后的物体。此情况决定只能先渲染不透明物体才能渲染透明物体

### 8.1 为什么渲染顺序很重要

- 若B的深度值比A深，即B在A的前面，A是半透明物体，B为任意透明的物体。
  1. 先渲染B后渲染A：若B是不透明物体，则其开启了深度测试，此时深度缓冲没有任何东西，将会写入深度和颜色缓冲，再渲染A时发现缓冲中已经有值了，将会进行混合得到正确结果。若B是半透明物体，其没有开启深度测试，但会先写入颜色缓冲，然后A渲染时进行混合得到正确结果
  2. 先渲染A后渲染B：若B是不透明物体，渲染A时是没有写入深度缓冲的，而后渲染B发现缓冲中没有值，将会覆盖掉A，导致A在B的后面，得到错误结果。若B是半透明物体，A和B都不会写入缓冲，但是混合顺序的不同结果也不会，也会造成物体B在A前面的假象，得到错误结果
- 渲染引擎一般会对物体进行排序再渲染。常见方法是先渲染所有不透明的物体，此时开启深度测试和深度写入。之后把半透明物体按它们距离摄像机的远近进行排序，然后按照从后往前的顺序进行渲染，此时开启深度测试，但关闭深度写入。
- 把半透明物体进行排序不能简单的按两个物体的深度进行排序，因为可能发生多个物体循环重叠的情况，一个物体并不是完全在另一个物体前面。这种问题的解决方式通常为分割网格

### 8.2 Unity Shader的渲染顺序

- unity为了解决渲染顺序的问题提供了渲染队列这一解决方案，可以使用SubShader的Queue标签决定模型位于哪个渲染队列。Unity在内部使用一系列整数索引来表示每个渲染队列，且索引号越小表示越早被渲染。

- Unity 5提前定义的5个渲染队列

  | 名称        | 队列索引号 | 描述                                                         |
  | ----------- | ---------- | ------------------------------------------------------------ |
  | Background  | 1000       | 这个渲染队列会在任何其他队列前被渲染，通常用该队列来渲染需要绘制在背景上的物体 |
  | Geometry    | 2000       | 默认的渲染队列，大多数物体和不透明物体使用这个队列           |
  | AlphaTest   | 2450       | 需要透明度测试的物体使用该队列                               |
  | Transparent | 3000       | 该队列物体会在所有Geometry和AlphaTest物体渲染后再从后往前渲染，使用了透明度混合的物体应该使用该队列 |
  | Overlay     | 4000       | 该队列用于实现一些叠加效果，任何需要在最后渲染的物体使用该队列 |

- ```c++
  SubShader{
  	// 更改渲染队列
  	Tags { "Queue" = "Transparent" }
      pass{
          ZWrite Off	// 关闭深度写入
      }
  }
  ```

### 8.3 透明度测试

- 通常在片元着色器中使用clip函数进行透明度测试。clip函数只接受一个参数，表示裁剪时使用的标量或矢量条件，可以是float4、float3、float2、float1、float中的任何一个类型。若给定参数的任何一个分量是负数，则会舍弃当前像素的颜色

```c++
Shader "Unity Shaders Book/Chapter 8/Alpha Test"{
	Properties{
		_Color("Color Tint", Color) = (1, 1, 1, 1)
		_MainTex("Main Tex", 2D) = "white" {}
		_Cutoff("Alpha Cutoff", Range(0, 1)) = 0.5
	}

	SubShader{
		Tags {"Queue" = "AlphaTest" "IngoreProjector" = "true" "RenderType" = "TransparentCutout"}

		pass{
			Tags { "LightMode" = "ForwardBase" }

			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag

			#include "Lighting.cginc"

			fixed4 _Color;
			sampler2D _MainTex;
			float4 _MainTex_ST;
			fixed _Cutoff;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float4 texcoord : TEXCOORD0;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
				float2 uv : TEXCOORD2;
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.worldNormal = normalize( UnityObjectToWorldNormal(v.normal));
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
				o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));

				fixed4 texColor = tex2D(_MainTex, i.uv);
				
				// Alpha Test
				clip(texColor.a - _Cutoff);
				// 上面等同于
				//if((texColor.a - _Cutoff) < 0.0){
				//	discard;
				//}

				fixed3 albedo = texColor.rgb * _Color.rgb;

				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;

				fixed3 diffuse = _LightColor0.rgb * albedo * saturate(dot(i.worldNormal, worldLightDir));

				fixed3 color = ambient + diffuse;
				return fixed4(color, 1.0);
			}
			ENDCG
		}
	}
	// 保证无法再当前显卡上无法工作时可以有合适的代替Shader，也可以保证使用透明测试的物体可以正确向其他物体投射阴影
	FallBack "TransparentCutout/VertexLit"
}
```

### 8.4 透明度混合

- ShaderLab的Blend命令

  | 语义                                             | 描述                                                         |
  | ------------------------------------------------ | ------------------------------------------------------------ |
  | Blend Off                                        | 关闭混合                                                     |
  | Blend SrcFactor DstFactor                        | 开启混合，并设置混合因子。源颜色会乘以SrcFactor，已经存在的颜色会乘以DSTFactor，然后两者相加后存入颜色缓存 |
  | Blend SrcFactor DstFactor, SrcFactorA DstFactorA | 对于rgb三个分量同上，对于Alpha分量使用SRCFactorA和DSTFactorA混合因子 |
  | BlendOp BlendOperation                           | 对源颜色和目标颜色使用BlendOperation后混合                   |

- Unity在使用Blend命令时自动打开了混合，

