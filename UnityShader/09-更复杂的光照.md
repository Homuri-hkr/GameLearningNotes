### 9.1 Unity的渲染路径

- 在Unity中渲染路径决定了光照是如何应用到UnityShader中的，若要和光源打交道，需要为每个Pass指定其使用的渲染路径，只有正确选择和设置了需要的渲染路径，Shader的光照计算才能被正确执行

- Unity 5.0前主要有三种渲染路径：Forward（前向渲染）、Deferrd（延迟渲染）、Vertex Lit（顶点照明渲染）。但在5.0版本后顶点照明路径已经被抛弃，并提供了新的延迟渲染。但还是可以选择之前老的渲染路径，若当前显卡不支持当前的渲染路径，则会变为更低一级的渲染路径，如不支持延迟渲染，则变为前向渲染

- 一般情况下，一个项目只使用一种渲染路径，可以为整个项目设置渲染时的路径，可以在Edit->Project Settings->Graphics->Tier Settings->Rendering Path中进行设置，默认为前向渲染

- 可以在相机Insperctor面板中设置Rendering Path覆盖项目的渲染路径，实现不同摄像机拥有不同的渲染路径

- 可在Unity Shader的Pass中使用LightMode为每个Pass设置渲染路径，若不指定渲染路径，一些光照变量可能不会被正确赋值，计算出的效果就会是错误的

- LightMode支持的渲染路径设置选项

  | 标签名               | 描述                                                         |
  | -------------------- | ------------------------------------------------------------ |
  | Always               | 不管使用哪种渲染路径，该Pass总是被渲染，但不会计算任何光照   |
  | ForwardBase          | 用于前向渲染，该Pass会计算环境光、平行光、逐顶点/SH光源和Lightmaps |
  | ForwardAdd           | 用于前向渲染，该Pass会计算额外的逐像素光源，每个Pass对应一个光源 |
  | Defferred            | 用于延迟渲染，该Pass会渲染G缓冲                              |
  | ShadowCaster         | 把物体的深度信息渲染到阴影映射纹理或一张深度纹理中           |
  | PrepassBase          | 用于遗留的延迟渲染。该Pass会渲染法线和高光反射的指数部分     |
  | PrepassFinal         | 用于遗留的延迟渲染。该Pass通过合并纹理、光照和自发光来渲染得到最后的颜色 |
  | Vertex、VertexLMRGBM | 用于遗留的顶点照明渲染                                       |

#### 9.1.1 前向渲染路径

**前向渲染路径的原理**

- 每进行完整的前向渲染需要渲染该对象的渲染图元，并计算两个缓冲区的信息，包括颜色缓冲区以及深度缓冲区，利用深度缓冲判断片元是否可见，若可见则更新颜色缓冲区的值，否则丢弃
- 对于每个逐像素的光源，都需要进行一次完整的前向渲染流程，若一个物体在多个逐像素光源的影响区域，则需要执行多个Pass， 每个Pass计算一个逐像素光源的光照结果，最后帧缓冲中将这些光照结果混合起来得到最终的颜色值。渲染引擎通常会限制每个物体的逐像素光照的数目

**Unity中的前向渲染**

- 一个Pass不仅仅可以用来计算逐像素光照，也可以用来计算逐顶点等其他光照，这取决于光照计算所处的流水线阶段以及计算时使用的数学模型

- 当渲染一个物体时，Unity会计算哪些光源照亮了它，以及这些光源照亮物体的方式

- Unity中，前向渲染路径有3种处理光照的方式：逐顶点处理、逐像素处理、球谐函数（SH）处理。

- 决定一个光源使用哪种处理模式取决于：

  1. 光源类型（Type）：光源是平行光还是其他类型的光源
  2. 光源的渲染模式（Render Mode）：该光照是否是重要的

  这两个可以在光的Inspector面板中进行设置

- 在前向渲染中，渲染一个物体时Unity会根据场景中各个光源的设置和对物体的影响程度对光源进行一个重要度排序。一些数目的光源会按逐像素的方式处理，最多4个光源按逐顶点方式处理，剩下的可以按SH方式处理：

  - 场景中最亮的平行光总是按逐像素处理的
  - 渲染模式被设置成Not Important的光源会按逐顶点或SH处理
  - 渲染模式被设置成Important的光源会按逐像素处理
  - 若根据上述规则得到的逐像素光源数量小于Quality Setting中的逐像素光源数量（Pixel Light Count），会有更多的光源以逐像素的方式进行渲染

- 前向渲染的两种Pass：

  1. Base Pass：光照计算会计算一个逐像素的平行光以及所有逐顶点和SH光源。可以实现光照纹理、环境光、自发光、平行光阴影，环境光和自发光是在BasePass中计算，因为对于一个物体，这两种光照只用计算一次，在Additional中进行计算就会叠加多次

     ```c++
     Tags { "LightMode" = "ForwardBase" }
     // 这种编译指令可以保证Unity可以为相应类型的Pass生成所有需要的Shader变种，这些变种会处理不同条件下的渲染逻辑
     #pragma multi_compile_fwdbase
     ```

  2. Additional Pass：光照计算会计算其他影响该物体的逐像素光源，每个光源执行一次Pass。默认情况下不支持阴影，可以通过编译指令开启

     ```c++
     Tags { "LightMode" = "ForwardAdd" }
     // 希望该Pass中得到的光照结果与帧缓冲的进行叠加，从而得到多个光照的渲染结果，若不开启，则会覆盖掉之前的结果
     Blend One One
     // 可以使用multi_compile_fwdadd_fullshadows代替该指令，为点光源和聚光源开启阴影效果，但这需要Unity在内部使用更多的Shader变种
     #pragma multi_compile_fwdadd
     ```

  对于前向渲染，一个Shader通常会定义一个BasePass（如双面渲染等则可以定义多个）以及一个Additional Pass。一个Base Pass仅会执行一次（多Base Pass除外），Additional Pass会根据影响该物体的其他逐像素光源的数目被多次调用，即每个逐像素光源执行一次Additional Pass

**内置的光照变量和函数**

- 前向渲染可以使用的内置光照变量

  | 名称                                                    | 类型     | 描述                                                         |
  | ------------------------------------------------------- | -------- | ------------------------------------------------------------ |
  | _LightColor0                                            | float4   | 该Pass处理的逐像素光源的颜色                                 |
  | _WorldSpaceLightPos0                                    | float4   | 该像素处理的逐像素光源的位置。若光源是平行光则w分量是0，其他光源w分量为1 |
  | _LightMatrix0                                           | float4x4 | 从世界空间到光源空间的变换矩阵，可以用于采样cookie和光强衰减纹理 |
  | unity_4LightPosX0，unity_4LightPosY0，unity_4LightPosZ0 | float4   | 仅用于Base Pass，前4个非重要的点光源在世界空间中的位置       |
  | unity_4LightAtten0                                      | float4   | 仅用于Base Pass，前4个非重要的点光源的衰减因子               |
  | unity_LightColor                                        | half4[4] | 仅用于Base Pass，前4个非重要的点光源的颜色                   |

#### 9.1.2 顶点照明渲染路径

- 顶点渲染路径是对硬件配置要求最少、运算性能最高，但也是效果最差的一种类型，其不支持逐像素才能得到的效果，如阴影、法线映射、高精度的高光反射等。实际上其仅仅是前向渲染路径中的一个自己，所有可以在顶点照明渲染路径中完成的都可以在前向渲染路径中完成

**Unity中的顶点照明光照渲染**

- 顶点照明渲染路径通常在一个Pass中完成对物体的渲染，且按逐顶点方式处理光照对物体的照明，其有最广泛的硬件支持（除了游戏机）

**可访问的内置变量的函数**

- 在Unity中，一个顶点照明的Pass中最多访问到8个逐顶点光源

- 顶点照明渲染路径中可以使用的内置变量

  | 名称                | 类型      | 描述                                                         |
  | ------------------- | --------- | ------------------------------------------------------------ |
  | unity_LightColor    | half4[8]  | 光源颜色                                                     |
  | unity_LightPosition | float4[8] | xyz分量是视角空间中的光源位置，若光源是平行光则z分量为0，其他光源z分量为1 |
  | unity_LightAtten    | half4[8]  | 光源衰减因子。若光源是聚光，x分量是cos(spotAngle/2)，y分量是1/cos(spotAngle/4)，若是其他光源，x分量是-1，y分量是1,z分量是衰减的平方，w分量是光源范围开根号的结果 |
  | unity_SpotDirection | float4[8] | 若光源是聚光灯，值为视角空间的聚光灯的位置，若时其他类型光源，值为（0,0,1,0） |

- 顶点照明渲染路径中可以使用的内置函数

  | 函数名                                                       | 描述                                                         |
  | ------------------------------------------------------------ | ------------------------------------------------------------ |
  | float3 ShadeVertexLights(float4 vertex, float3 normal)       | 输入模型空间中的顶点位置和发现，计算四个逐顶点光源的光照以及环境光。内部实现调用了ShadeVertexLightsFull |
  | float3 ShadeVertexLightsFull(float4 vertex, float3 normal, int lightCount, bool spotLight) | 输入模型空间的顶点位置和法线，计算lightCount个光源的光照以及环境光。若spotLight值为true，则这些光源会被当成聚光灯处理，虽然结果更精确但计算更耗时。否则按点光源处理 |

#### 9.1.3 延迟渲染路径

- 前向渲染的问题是：场景中包含大量实时光源时，光源影响的区域互相重叠，为了得到光照效果需要为区域内的每个物体执行多个Pass计算不同光源对该物体的光照结果然后进行混合。但每执行一个Pass都需要重新渲染一遍物体，很多计算是重复的
- 延迟渲染是一种更古老的渲染方法，因为前向渲染的问题而流行起来。其会利用额外的缓冲区G-buffer，存储所有离摄像机最近的表面的其他信息，如法线、位置、用于光照计算的材质属性等

**延迟渲染的原理**

- 延迟渲染的大致流程为：在第一个Pass中不进行任何光照计算，而是计算哪些片元是可见的，并将相关信息存入G-buffer中，对每个物体来说该Pass只执行一次。在第二个Pass中利用G-buffer中的信息进行真正的光照计算
- 延迟渲染使用的Pass通常为两个，其效率不依赖于场景的复杂度，而是使用的屏幕空间的大小有关

**Unity中的延迟渲染**

- Unity有两种延迟渲染路径，一种是Unity 5前遗留的延迟渲染路径，一种是Unity 5后使用的延迟渲染路径。若游戏中使用了大量的实时光照，则可能洗碗选择延迟渲染路径，但该路径需要一定的硬件支持
- 新旧延迟渲染路径差别很小，只是使用不同的技术来权衡不同的需求
- 延迟渲染的缺点：
  - 不支持真正的抗锯齿功能
  - 不能处理半透明物体
  - 对显卡有一定的要求
- 默认的G-buffer包含以下渲染纹理（不同版本的渲染纹理存储可能内容不同）：
  - RT0：格式为ARGB32，RGB通道用于存储漫反射颜色，A通道没有使用
  - RT1：格式为ARGB32，RGB通道用于存储高光反射颜色，A通道存储高光反射的指数部分
  - RT2：格式为ARGB2101010，RGB通道用于存储法线，A通道没有使用
  - RT3：格式为ARGB32（非HDR）或ARGBHalf（HDR），用于存储自发光+lightmap+发射探针
  - 深度缓冲和模板缓冲
- Unity官方文档（docs.unity3d/Manual/RanderingPaths.html）给出了4种渲染路径的详细比较

**可访问的内置变量和函数**

- 延迟渲染路径中可使用的内置变量

  | 名称          | 类型     | 描述                                                         |
  | ------------- | -------- | ------------------------------------------------------------ |
  | _LightColor   | float4   | 光源颜色                                                     |
  | _LightMatrix0 | float4x4 | 从世界空间到光源空间的变换矩阵。可用于采样cookie和光强衰减纹理 |

### 9.2 Unity的光源类型

- Unity一共支持4种光源类型：平行光、点光源、聚光源、面光源。其中面光源仅在烘焙时才可发挥作用

#### 9.2.1 光源类型有什么影响

**平行光**

- 平行光没有位置，几何属性只有方向，其到场景中所有点的方向都是一样的。由于其没有具体位置，所以没有衰减概念，即光强不会随着距离而发生改变

**点光源**

- 点光源由空间中一个球体定义，可以表示由一个点发出的、向所有方向延伸的光。其具有位置，点光源会进行衰减，球心处光强最强，边界最弱

**聚光源**

- 聚光源由空间中的一块锥形区域定义，可以表示为由一个特定位置出发、向特定方向延伸的光。其具有位置，聚光源会进行衰减，顶点处光强最强，锥形边界处强度为0

#### 9.2.2 在前向渲染中处理不同的光源类型

```c++
Shader "Unity Shaders Book/Chapter 9/Forward Rendering"{

	Properties{
		_Color ("Color Tint", Color) = (1, 1, 1, 1)
		_Diffuse ("Diffuse", Color) = (1, 1, 1, 1)
		_Specular ("Specular", Color) = (1, 1, 1, 1)
		_Gloss ("Gloss", Range(8.0, 256)) = 20
	}

	SubShader{
		
		// Base Pass，处理最亮的平行光
		Pass{
			Tags { "LightMode" = "ForwardBase" }

			CGPROGRAM
			
			#pragma multi_compile_fwdbase
			#pragma vertex vert 
			#pragma fragment frag 

			#include "Lighting.cginc"

			fixed4 _Color;
			fixed4 _Diffuse;
			fixed4 _Specular;
			fixed _Gloss;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;

				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
				fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(i.worldNormal, worldLightDir));

				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
				fixed3 halfDir = normalize(worldLightDir + viewDir);
				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(i.worldNormal, halfDir)), _Gloss);
				// 平行光的衰减
				fixed atten = 1.0;

				fixed3 color = ambient + (diffuse + specular) * atten;
				return fixed4(color, 1.0);
			}

			ENDCG
		}
		// Additional Pass，处理其他逐像素光源
		// 基本和Base Pass相同，但要去掉环境光、自发光、逐顶点光照、SH光照部分等只需计算一次的部分，并添加对不同光源类型的支持
		Pass{
			Tags { "LightMode" = "ForwardAdd" }

			Blend One One

			CGPROGRAM
			
			#pragma multi_compile_fwdadd
			#pragma vertex vert 
			#pragma fragment frag 

			#include "Lighting.cginc"
			#include "AutoLight.cginc"

			fixed4 _Color;
			fixed4 _Diffuse;
			fixed4 _Specular;
			fixed _Gloss;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				
				// 若当前光源为平行光，Unity底层渲染引擎就会定义该宏
				#ifdef USING_DIRECTIONAL_LIGHT
					fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);
				#else
				// 若为点光源或聚光源
					fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz - i.worldPos.xyz);
				#endif

				fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(i.worldNormal, worldLightDir));

				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
				fixed3 halfDir = normalize(worldLightDir + viewDir);
				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(i.worldNormal, halfDir)), _Gloss);

				#ifdef USING_DIRECTIONAL_LIGHT
				fixed atten = 1.0;
				#else
				float3 lightCoord = mul(unity_WorldToLight, float4(i.worldPos, 1)).xyz;
				fixed atten = tex2D(_LightTexture0, dot(lightCoord, lightCoord).rr).UNITY_ATTEN_CHANNEL;
				#endif

				fixed3 color = (diffuse + specular) * atten;
				return fixed4(color, 1.0);
			}
			ENDCG
		}
	}
	FallBack "Specular"
}
```

### 9.3 Unity的光照衰减

- Unity使用一张纹理作为查找表来在片元着色器中计算逐像素光照的衰减，其好处在于计算衰减不依赖数学公式的复杂性，只需用一个参数值取纹理中采样即可。但也有些弊端：
  - 需要预处理得到采样纹理，且纹理的大小也会影响衰减的精度
  - 不直观且不方便，一旦把数据存储到查找表中就无法使用其他数学公式来计算衰减

#### 9.3.1 用于光照衰减的纹理

- Unity在内部使用一张名为_LightTexture0的纹理来计算光源衰减。若对某光源使用了cookie，则衰减查找纹理是\_LightTexture0。

- 在_LightTexture0中，（0,0）表示和光源位置重合点的衰减值，（1,1）表示在光源空间中关心的距离最远的衰减值。为了得到给定点到光源的衰减值，首先需要得到在该点在光源空间中的位置

  ```c++
  float3 lightCoord = mul(_LightMatrix0, float4(i.worldPos, 1)).xyz;
  ```

  然后通过坐标的模的平方对衰减纹理采样，不使用距离的长度可以避免开方操作。使用宏UNITY_ATTEN_CHANNEL获得衰减纹理中衰减值所在的分量

  ```c++
  fixed atten = tex2D(_LightTexture0, dot(lightCoord, lightCoord).rr).UNITY_ATTEN_CHANNEL;
  ```

#### 9.3.2 使用数学公式计算衰减

- Unity没有在文档中给出内置衰减计算的相关说明，尽管可以利用一些数学公式计算衰减，但由于无法在Shader中通过内置变量得到光源的范围、聚光灯朝向、张开角度等信息，得到的效果不尽人意。

### 9.4 Unity的阴影

#### 9.4.1 阴影是如何实现的

- Shadow Map：先将摄像机的位置放在与光源重合的位置上，场景中该光源的阴影区域就是那些摄像机看不到的地方

- 在前向渲染路径中，若场景中最重要的平行光开启了阴影，Unity就会为该光源计算其阴影映射纹理，该纹理本质上是一张深度图，记录了从该光源的位置触发、能看到的场景中距离它最近的表面位置

- 计算阴影映射纹理时，如何判断距离其最近的表面位置：

  1. 先把摄像机放置到光源位置上，然后以正常的渲染流程调用Base Pass和Additional Pass更新深度信息，得到阴影映射纹理。该方法会对性能造成一定的影响，真正需要的仅仅是深度信息，但这两个Pass往往涉及很多复杂的光照运算
  2. Unity提供一个额外的Pass专门更新光源的阴影映射纹理，即将LightMode设为ShadowCaster，该Pass渲染目标不是帧缓存而是阴影映射纹理。Unity首相将摄像机放置到光源位置上调用该Pass。当开启光源的阴影效果后，底层渲染引擎首先会在当前渲染物体的Unity中找到LightMode为ShadowCaster的Pass，若没有则在Fallback指定的UnityShader中找，若还没有则无法投射阴影

- 传统阴影映射纹理实现中，会在正常渲染的Pass中把顶点位置变换到光源空间下，以得到在光源空间中的三维位置信息，然后用xy分量对阴影映射纹理进行采样得到阴影映射纹理中该位置的深度信息，若深度值小于顶点的深度值则该点位于阴影中

- Unity 5使用了屏幕空间的阴影映射技术（Screenspace Shadow Map），其原本是延迟渲染中产生阴影的方法，该技术需要显卡支持MRT，有些移动平台不支持。该技术会首先调用ShadowCaster的Pass得到可投射阴影的光源的阴影映射纹理以及摄像机的深度纹理，然后根据光源的阴影映射纹理和摄像机的深度纹理来得到屏幕空间中的阴影图，若摄像机的深度图中记录的表面深度大于转换到阴影映射纹理中的深度值，说明该表面虽是可见的但存在于阴影中。阴影图就包含了屏幕空间中所有阴影的区域，若想要一个物体接受来自其他物体的阴影，只需在Shader中对阴影图进行采样。由于阴影图是屏幕空间下的，首先需要把表面坐标从模型空间变换到屏幕空间再进行采样

  一个物体接收来自其他物体的阴影和向其他物体投射阴影是两个过程：

  1. 若要接收阴影，必须在Shader中对阴影映射纹理包括屏幕空间的阴影图进行采样，将采样结果和最后的光照结果相乘产生阴影效果
  2. 若要投射阴影，必须将该物体加入到光源的阴影映射纹理计算中，从而让其他物体在对阴影映射纹理采样时可以得到该物体的相关信息

#### 9.4.2 不透明物体的阴影

- Unity中，在物体的Mesh组件中通过Cast Shadows属性和Receive Shadows属性选择让物体投射阴影和接受阴影。表面的反面是不会添加到阴影映射的纹理中的，这对于只有一个正面的平面在某些情况下就不适合，可以在Cast Shadows属性中设为Two Sided来允许对物体所有面都计算阴影信息
- 在内置Shader的Specular拥有ShadowCaster的Pass。Diffuse中本身不含该Pass，但其Fallback调用了VertexLit，该Shader中拥有

```c++
Shader "Unity Shaders Book/Chapter 9/Shadow"{

	Properties{
		_Color ("Color Tint", Color) = (1, 1, 1, 1)
		_Diffuse ("Diffuse", Color) = (1, 1, 1, 1)
		_Specular ("Specular", Color) = (1, 1, 1, 1)
		_Gloss ("Gloss", Range(8.0, 256)) = 20
	}

	SubShader{
		
		// Base Pass，处理最亮的平行光
		Pass{
			Tags { "LightMode" = "ForwardBase" }

			CGPROGRAM
			
			#pragma multi_compile_fwdbase
			#pragma vertex vert 
			#pragma fragment frag 

			#include "Lighting.cginc"
			// 下面使用的用于阴影映射的三个宏来源于该文件
			// 这三个宏会使用上下文变量来进行相关计算，为了让这些宏正确工作，需要保证自定义变量名与宏中使用的变量名匹配
			// 保证：a2v结构体的顶点坐标变量名为vertex，顶点着色器输入结构体a2v的变量名必须为v，v2f中顶点位置变量必须为pos
			#include "AutoLight.cginc"

			fixed4 _Color;
			fixed4 _Diffuse;
			fixed4 _Specular;
			fixed _Gloss;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
				// 声明一个用于对阴影纹理采样的坐标。该宏的参数是下一个可用的插值寄存器的索引值
				// 实际声明了一个名为_ShadowCoord的阴影纹理坐标变量
				SHADOW_COORDS(2)
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

				// 用于在顶点着色器中计算上一步中声明的阴影纹理坐标
				// 实现根据平台而已，若当前平台可以使用屏幕空间打的阴影映射技术则会调用内置的ComputeScreenPos函数计算_ShadowCoord，否则使用传统阴影映射技术
				// 其会把顶点坐标从模型空间变换到光源空间后存储到_ShadowCoord中
				TRANSFER_SHADOW(o);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				// 负责使用_ShadowCoord对相关纹理采样，得到阴影信息
				fixed shadow = SHADOW_ATTENUATION(i);

				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;

				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
				fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(i.worldNormal, worldLightDir));

				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
				fixed3 halfDir = normalize(worldLightDir + viewDir);
				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(i.worldNormal, halfDir)), _Gloss);
				// 平行光的衰减
				fixed atten = 1.0;

				fixed3 color = ambient + (diffuse + specular) * atten * shadow;
				return fixed4(color, 1.0);
			}

			ENDCG
		}
		// Additional Pass，处理其他逐像素光源
		// 基本和Base Pass相同，但要去掉环境光、自发光、逐顶点光照、SH光照部分等只需计算一次的部分，并添加对不同光源类型的支持
		Pass{
			Tags { "LightMode" = "ForwardAdd" }

			Blend One One

			CGPROGRAM
			// 用multi_compile_fwdadd_fullshadows替换multi_compile_fwdadd来让ForwardAdd Pass支持阴影
			#pragma multi_compile_fwdadd_fullshadows
			#pragma vertex vert 
			#pragma fragment frag 

			#include "Lighting.cginc"
			#include "AutoLight.cginc"

			fixed4 _Color;
			fixed4 _Diffuse;
			fixed4 _Specular;
			fixed _Gloss;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
				SHADOW_COORDS(2)
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
				TRANSFER_SHADOW(o);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{

				fixed shadow = SHADOW_ATTENUATION(i);
				
				// 若当前光源为平行光，Unity底层渲染引擎就会定义该宏
				#ifdef USING_DIRECTIONAL_LIGHT
					fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);
				#else
				// 若为点光源或聚光源
					fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz - i.worldPos.xyz);
				#endif

				fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(i.worldNormal, worldLightDir));

				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
				fixed3 halfDir = normalize(worldLightDir + viewDir);
				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(i.worldNormal, halfDir)), _Gloss);

				#ifdef USING_DIRECTIONAL_LIGHT
				fixed atten = 1.0;
				#else
				float3 lightCoord = mul(unity_WorldToLight, float4(i.worldPos, 1)).xyz;
				fixed atten = tex2D(_LightTexture0, dot(lightCoord, lightCoord).rr).UNITY_ATTEN_CHANNEL;
				#endif

				fixed3 color = (diffuse + specular) * atten * shadow;
				return fixed4(color, 1.0);
			}
			ENDCG
		}
	}
	FallBack "Specular"
}
```

#### 9.1.4 统一管理光照衰减和阴影

```c++
Shader "Unity Shaders Book/Chapter 9/Attenuation And Shadow"{

	Properties{
		_Color ("Color Tint", Color) = (1, 1, 1, 1)
		_Diffuse ("Diffuse", Color) = (1, 1, 1, 1)
		_Specular ("Specular", Color) = (1, 1, 1, 1)
		_Gloss ("Gloss", Range(8.0, 256)) = 20
	}

	SubShader{
		
		// Base Pass，处理最亮的平行光
		Pass{
			Tags { "LightMode" = "ForwardBase" }

			CGPROGRAM
			
			#pragma multi_compile_fwdbase
			#pragma vertex vert 
			#pragma fragment frag 

			#include "Lighting.cginc"
			// 下面使用的用于阴影映射的三个宏来源于该文件
			// 这三个宏会使用上下文变量来进行相关计算，为了让这些宏正确工作，需要保证自定义变量名与宏中使用的变量名匹配
			// 保证：a2v结构体的顶点坐标变量名为vertex，顶点着色器输入结构体a2v的变量名必须为v，v2f中顶点位置变量必须为pos
			#include "AutoLight.cginc"

			fixed4 _Color;
			fixed4 _Diffuse;
			fixed4 _Specular;
			fixed _Gloss;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
				// 声明一个用于对阴影纹理采样的坐标。该宏的参数是下一个可用的插值寄存器的索引值
				// 实际声明了一个名为_ShadowCoord的阴影纹理坐标变量
				SHADOW_COORDS(2)
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

				// 用于在顶点着色器中计算上一步中声明的阴影纹理坐标
				// 实现根据平台而已，若当前平台可以使用屏幕空间打的阴影映射技术则会调用内置的ComputeScreenPos函数计算_ShadowCoord，否则使用传统阴影映射技术
				// 其会把顶点坐标从模型空间变换到光源空间后存储到_ShadowCoord中
				TRANSFER_SHADOW(o);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				// 同时计算光照衰减和阴影，atten参数由该宏进行声明
				// AutoLight.cginc中根据不同情况声明了多个版本的UNITY_LIGHT_ATTENUATION
				UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);

				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;

				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
				fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(i.worldNormal, worldLightDir));

				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
				fixed3 halfDir = normalize(worldLightDir + viewDir);
				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(i.worldNormal, halfDir)), _Gloss);

				fixed3 color = ambient + (diffuse + specular) * atten;
				return fixed4(color, 1.0);
			}

			ENDCG
		}
		// Additional Pass，处理其他逐像素光源
		// 基本和Base Pass相同，但要去掉环境光、自发光、逐顶点光照、SH光照部分等只需计算一次的部分，并添加对不同光源类型的支持
		Pass{
			Tags { "LightMode" = "ForwardAdd" }

			Blend One One

			CGPROGRAM
			
			// 用multi_compile_fwdadd_fullshadows替换multi_compile_fwdadd来让ForwardAdd Pass支持阴影
			#pragma multi_compile_fwdadd_fullshadows
			#pragma vertex vert 
			#pragma fragment frag 

			#include "Lighting.cginc"
			#include "AutoLight.cginc"

			fixed4 _Color;
			fixed4 _Diffuse;
			fixed4 _Specular;
			fixed _Gloss;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
				SHADOW_COORDS(2)
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
				TRANSFER_SHADOW(o);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{

				UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
				
				// 若当前光源为平行光，Unity底层渲染引擎就会定义该宏
				#ifdef USING_DIRECTIONAL_LIGHT
					fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);
				#else
				// 若为点光源或聚光源
					fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz - i.worldPos.xyz);
				#endif

				fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(i.worldNormal, worldLightDir));

				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
				fixed3 halfDir = normalize(worldLightDir + viewDir);
				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(i.worldNormal, halfDir)), _Gloss);

				fixed3 color = (diffuse + specular) * atten;
				return fixed4(color, 1.0);
			}
			ENDCG
		}
	}
	FallBack "Specular"
}
```

#### 9.1.5 透明度物体的阴影

- 对于大多数不透明物体来说，将Fallback设置为VertexLit就可以得到正确的阴影，但对于透明物体来说需要小心进行处理。若仍然直接使用VertexLit、Diffuse、Specular等作为回调往往无法得到正确的阴影，因为透明度测试需要在片元着色器舍弃某些片元，而在VertexLit的阴影投射中没有进行这样操作
- 要想为半透明物体产生正确的阴影，需要在每个光源空间下仍然严格按照从前往后的顺序进行渲染，让阴影处理变的非常复杂且影响性能。所以，在Unity中所有的内置半透明Shader是不会产生任何阴影效果的。当然也可以使用一些dirty trick强制为半透明物体生成阴影，通过将它们的Fallback设置为VertexLit这些对不透明物体使用的UnityShader