### 11.1 Unity Shader中的内置变量（时间）

- Unity Shader提供了一系列关于时间的内置变量允许在Shader中访问运行时间，实现各种动画效果

- Unity内置的时间变量

  | 名称            | 类型   | 描述                                                         |
  | --------------- | ------ | ------------------------------------------------------------ |
  | _Time           | float4 | t是该场景加载开始经过的时间，4个分量的值为（t/20，t，2t，3t） |
  | _SinTime        | float4 | t是时间的正弦值，4个分量值为（t/8，t/4，t/2，t）             |
  | _CosTime        | float4 | t是时间的余弦值，4个分量值为（t/8，t/4，t/2，t）             |
  | unity_DeltaTime | float4 | dt是时间增量，4个分量值为（dt，1/dt，smoothDt，1/smoothDt）  |

### 11.2 纹理动画

- 纹理动画在游戏中的应用非常广泛，往往会使用纹理动画代替复杂的例子系统等模拟各种动画效果

#### 11.2.1 序列帧动画

- 序列帧动画的原理是依次播放一系列关键帧图像，当播放速度到达一定数值时看上去就是一个连续的动画。其有点在于灵活性很强，不需要进行任何物理计算就可以得到非常细腻的动画效果。缺点是序列帧中每张关键帧图像都不一样，要制作出一张出色的序列帧纹理需要的美术工程量比较大

```c++
Shader "Unity Shaders Book/Chapter 11/Image Sequence Animation"{

	Properties{
		_Color("Color Tint", Color) = (1, 1, 1, 1)
		// 包含了所有关键帧图像的纹理
		_MainTex("Image Sequence", 2D) = "white" {}
		// 图像在水平和数值方向包含的关键帧图像个数
		_HorizontalAmount("Horizontal Amount", Float) = 4
		_VerticalAmount("Vertical Amount", Float) = 4
		// 序列帧动画的播放速度
		_Speed("Speed", Range(1, 100)) = 30
	}

	SubShader{
		// 序列帧图像通常是透明纹理，所以需要设置相关状态
		Tags {"Queue" = "Transparent" "IgnoreProjector" = "true" "RenderType" = "Transparent"}

		Pass{
			Tags{ "LightMode" = "ForwardBase" }
			
			ZWrite Off 
			Blend SrcAlpha OneMinusSrcAlpha

			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag 

			#include "UnityCG.cginc"

			fixed4 _Color;
			sampler2D _MainTex;
			fixed4 _MainTex_ST;
			float _HorizontalAmount;
			float _VerticalAmount;
			fixed _Speed;

			struct a2v {
				float4 vertex : POSITION;
				float2 texcoord : TEXCOORD0;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float2 uv : TEXCOORD0;
			};

			v2f vert(a2v v){
				v2f o;
				
				o.pos = UnityObjectToClipPos(v.vertex);
				o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				// 获取当前时间
				float time = floor(_Time.y * _Speed);
				// 行索引
				float row = floor(time / _HorizontalAmount);
				// 列索引
				float column = time - row * _HorizontalAmount;

				// unity的uv坐标原点为左下角
				// 动画播放从左到右从上到下
				half2 uv = i.uv + half2(column, -row);
				uv.x /= _HorizontalAmount;
				uv.y /= _VerticalAmount;

				fixed4 color = tex2D(_MainTex, uv);
				color.rgb *= _Color;
				return color;
			}
			ENDCG
		}
	}
	Fallback "Transparent/VertexLit"
}
```

#### 11.2.2 滚动的背景

- 很多2D游戏使用了不断滚动的背景来模拟游戏角色在场景中的穿梭，这些背景往往包含了多个层来模拟一种视差效果，而背景的实现往往利用纹理动画

```c++
Shader "Unity Shaders Book/Chapter 11/Scrolling Background"{
	
	Properties{
		_MainTex("Base Layer (RGB)", 2D) = "white" {}
		_DetailTex("2nd Layer (RGB)", 2D) = "white" {}
		_ScrollX("Base layer Scroll Speed", Float) = 1.0
		_Scroll2X("2nd Layer Scroll Speed", Float) = 1.0
		_Multiplier("Layer Multiplier", Float) = 1
	}

	SubShader{
		Tags { "RenderType"="Opaque" "Queue"="Geometry"}
		
		Pass{
			Tags { "LightMode"="ForwardBase" }
			
			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag 

			#include "UnityCG.cginc"

			sampler2D _MainTex;
			sampler2D _DetailTex;
			float4 _MainTex_ST;
			float4 _DetailTex_ST;
			float _ScrollX;
			float _Scroll2X;
			float _Multiplier;
			
			struct a2v {
				float4 vertex : POSITION;
				float4 texcoord : TEXCOORD0;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float4 uv : TEXCOORD0;
			};

			v2f vert(a2v v){
				v2f o;
				
				o.pos = UnityObjectToClipPos(v.vertex);
				// 使用时间在水平方向上对纹理坐标进行偏移，以此达到滚动
				o.uv.xy = TRANSFORM_TEX(v.texcoord, _MainTex) + frac(float2(_ScrollX, 0.0) * _Time.y);
				o.uv.zw = TRANSFORM_TEX(v.texcoord, _DetailTex) + frac(float2(_Scroll2X, 0.0) * _Time.y);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				fixed4 firstLayer = tex2D(_MainTex, i.uv.xy);
				fixed4 secondLayer = tex2D(_DetailTex, i.uv.zw);

				fixed4 color = lerp(firstLayer, secondLayer, secondLayer.a);
				color.rgb *= _Multiplier;
				
				return color;
			}

			ENDCG
		}
	}
	FallBack "VertexLit" 
}
```

### 11.3 顶点动画

#### 11.3.1 流动的河流

- 河流的模拟通常是使用正弦函数等来模拟水流的波动效果

```c++
// Upgrade NOTE: replaced 'mul(UNITY_MATRIX_MVP,*)' with 'UnityObjectToClipPos(*)'

Shader "Unity Shaders Book/Chapter 11/Water"{
	
	Properties{
		// 河流纹理
		_MainTex("Main Tex", 2D) = "white" {}
		_Color("Color Tint", Color) = (1, 1, 1, 1)
		// 控制水流波动的幅度
		_Magnitude("Distortion Magnitude", Float) = 1
		// 控制波动频率
		_Frequency("Distortion Frequency", Float) = 1
		// 控制波长的倒数
		_InvWaveLength("Distortion Inverse Wave Length", Float) = 10
		// 控制河流纹理移动速度
		_Speed("Speed", Float) = 0.5
	}

	SubShader{
		// 包含了模型空间的顶顶啊动画的Shader，预处理会合并所有相关的模式，这些模型各自的模型空间就会丢失，所以要禁用批处理
		Tags {"Queue" = "Transparent" "IgnoreProjector" = "true" "RenderType" = "Transparent" "DisableBatching" = "true"}
		
		Pass{
			Tags { "LightMode" = "ForwardBase" }
		
			ZWrite Off 
			Blend SrcAlpha OneMinusSrcAlpha
			// 关闭剔除，让每个水流都能显示
			Cull Off 


			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag 

			#include "UnityCG.cginc"

			sampler2D _MainTex;
			float4 _MainTex_ST;
			fixed4 _Color;
			float _Magnitude;
			float _Frequency;
			float _InvWaveLength;
			float _Speed;

			struct a2v {
				float4 vertex : POSITION;
				float4 texcoord : TEXCOORD0;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float2 uv : TEXCOORD0;
			};

			v2f vert(a2v v){
				v2f o;

				float4 offset;
				offset.yzw = float3(0.0, 0.0, 0.0);
				// 使用加法是让不同的位置波动的不一样，不在同一水平线上
				offset.x = sin(_Frequency * _Time.y + v.vertex.x * _InvWaveLength + v.vertex.y * _InvWaveLength + v.vertex.z * _InvWaveLength) * _Magnitude;
				o.pos = UnityObjectToClipPos(v.vertex + offset);
				
				o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);
				o.uv += float2(0.0, _Time.y * _Speed);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				fixed4 color = tex2D(_MainTex, i.uv);
				color.rgb *= _Color.rgb;
			
				return color;
			}

			ENDCG
		}
	}
	FallBack "Transparent/VertexLit"
}
```

#### 11.3.2 广告牌

- 广告牌技术会根据视角方向来旋转一个被纹理着色的多边形（通常为简单的四边形），使得多边形看起来总是面对摄像机。被应用于如渲染烟雾、云朵、闪光效果等。本质是构建旋转矩阵，其使用的基向量就是表面法线，向上的方向以及向右的方向，此外还需要指定一个锚点，该锚点在旋转过程中是固定不变的，依次来确定多边形在空间中的位置。
- 广告牌技术重点在于根据需求构建3个相互正交的基向量。计算过程通常是，首先会通过出式计算得到目标的表面法线和指向上的方向，而两者往往是不垂直的，但其中之一是固定的。假设法线是固定的，首先根据法线和指向上的方向计算出指向右的方向，归一化后计算出正交指向上的方向

```c++
Shader "Unity Shaders Book/Chapter 11/Billboard"{
	
	Properties{
		// 广告牌显示的透明纹理
		_MainTex("Main Tex", 2D) = "white" {}
		_Color("Color Tint", Color) = (1, 1, 1, 1)
		// 用于调整是固定发现还是固定指向上的方向，即约束垂直方向的程度
		_VerticalBillboarding("Vertical Restraints", Range(0, 1)) = 1
	}

	SubShader{
		Tags{ "Queue" = "Transparent" "IgnoreProjector" = "True" "RenderType" = "Transparent" "DisableBatching" = "True"}

		Pass{
			Tags {"LightMode" = "ForwardBase"}
			
			ZWrite Off
			Blend SrcAlpha OneMinusSrcAlpha
			Cull Off 

			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag 
			#include "UnityCG.cginc"

			sampler2D _MainTex;
			float4 _MainTex_ST;
			float4 _Color;
			fixed _VerticalBillboarding;

			struct a2v {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float2 texcoord : TEXCOORD0;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float2 uv : TEXCOORD0;
			};

			v2f vert(a2v v){
				v2f o;

				// 以模型空间原点为锚点
				float3 center = float3(0, 0, 0);
				// 模型空间下视角位置
				float3 viewer = mul(unity_WorldToObject, float4(_WorldSpaceCameraPos, 1));
				// 求法线
				float3 normalDir = viewer - center;
				normalDir.y = normalDir.y * _VerticalBillboarding;
				normalDir = normalize(normalDir);
				// 求上、右方向构建正交基
				float3 upDir = abs(normalDir.y) > 0.999 ? float3(0, 0, 1) : float3(0, 1, 0);
				float3 rightDir = normalize(cross(upDir, normalDir));
				upDir = normalize(cross(normalDir, rightDir));

				float3 centerOffs = v.vertex.xyz - center;
				float3 localPos = center + rightDir * centerOffs.x + upDir * centerOffs.y + normalDir * centerOffs.z;

				o.pos = UnityObjectToClipPos(float4(localPos, 1.0));
				o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);

				
				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				fixed4 color = tex2D(_MainTex, i.uv);
				color.rgb *= _Color.rgb;
			
				return color;
			}

			ENDCG
		}
	}
	FallBack "Transparent/VertexLit"
}
```

#### 11.3.3 注意事项

- 若在模型空间下进行一些顶点动画，批处理往往会破坏这种动画效果，可以通过SubShader的DisableBatching强制取药该UnityShader的批处理，但这回带来一定的性能下降，增加了DrawCall，所以应该精良避免使用模型空间下的一些绝对位置和方向进行计算。如为了避免显式使用模型空间的中心作为锚点，可以利用顶点颜色存储每个顶点到锚点的距离值
- 若想对包含了顶点动画的物体添加阴影，若使用内置的Shader的阴影Pass来进行渲染则得不到正确的阴影效果，因为那些ShadowCaster Pass没有进行相关的顶点动画，仍会按之前的顶点计算阴影。因此需要自定义的ShadowCaster Pass，并在其中进行同样的顶点变换过程