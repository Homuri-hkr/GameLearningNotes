### 14.1 卡通风格的渲染

- 卡通风格的画面通常有一些共有的特点，若物体被黑色的线条描边，以及分明的明暗变化。实现卡通渲染的方法有很多，其中之一是使用基于色调的着色技术，其往往会使用漫反射系数对一张一维纹理进行采样，以控制漫反射的色调。卡通风格中模型的高光往往是一块块分界明显的纯色区域，且卡通风格还需要在物体边缘部分绘制轮廓

**渲染轮廓线**

- 在实时渲染一书中，轮廓线的渲染方法分为5种类型：

  1. 基于观察角度和表面法线的轮廓线渲染，该方法使用视角方向和表面法线的点乘结果来得到轮廓线的信息，该方法简单快速，在一个Pass中就得到渲染结果，但局限性很大，很多模型渲染出来的描边效果不尽人意

  2. 过程式几何轮廓线渲染，该方法核心是使用两个Pass渲染。第一个Pass渲染背面的面片，并使用某些技术让它的轮廓可见，第二个Pass再正常渲染正面的面片，该方法的优点在于快速有效，且适用于绝大多数表面平滑的模型，但缺点是不适合立方体这样平整的模型

  3. 基于图像处理的轮廓线渲染。该方法的优点在可以适用于任何种类的模型，基于深度和法线的局限是一些深度和法线变化很小的轮廓无法被检测出来（如桌上的纸张）

  4. 基于轮廓边检测的轮廓线渲染。上面3个方法不能控制轮廓线的风格渲染，有时希望渲染出独特风格的轮廓线，为此希望检测出精确的轮廓边然后直接渲染。检测一条边是否为轮廓边只需要检查和这条边相邻的两个三角形面片是否满足如下条件，本质是检查两个相邻面片是否朝向同一个方向（同正或同背面）
     $$
     (n_0\cdot{}v>0) \neq{} (n_1 \cdot{} v > 0),n_0和n_1是两个相邻三角面片的法向量，v是从视角到该边任意顶点方向
     $$
     该方法的缺点是实现复杂，且有动作连贯性问题，在逐帧提取轮廓可能出现跳跃性

  5. 混合上述几种渲染方法。如先找到精确的轮廓边，将模型和轮廓边渲染到纹理中，再使用图像处理方法识别出轮廓线并在图像空间进行风格化渲染

```c++
// 过程式几何轮廓渲染
// 在第一个Pass中使用轮廓线颜色渲染整个背面的面片，并在视角空间下把模型顶点沿着光线方向向外扩张一段距离，让背部的轮廓线可见
// 但若直接使用顶点法线进行扩展对于一些内凹的模型可能发生背面面片遮挡正面的情况，为尽可能防止该情况，首先对顶点法线的z分量进行处理使其成为一个定值，然后把发现归一化后再进行扩张。这使扩展后的背面更加扁平化从而降低遮挡正面面片的可能性
viewNormal.z = -0.5;
viewNormal = normalize(viewNormal);
viewPos = viewPos + viewNormal * _Outline;
```

 **添加高光**

- 对于卡通渲染需要的高光反射光照模型，需要normal和halfDir的点乘结果，不同的是把该值和一个阈值进行比较，若小于该阈值，则高光反射系数为0，否则返回1

```c++
float spec = dot(worldNormal, worldHalfDir);
// step函数第一个参数是参考值，第二个是待比较的值，若第二个参数大于等于第一个参数返回1，否则返回0。在UnityCG.cginc中声明
spec = step(threshold, spec);
```

- 但该粗暴的判断方法会在高光区域的边界造成锯齿，原因在于边缘不是平滑渐变的，而是由0突变到1，可以在很小的区域进行平滑处理

```c++
float spec = dot(worldNormal, worldHalfDir);
// smoothstep函数，当参数3小于参数1时返回0，大于w返回1，在两者之间返回0和1的插值。在UnityCG.cginc中声明
spec = lerp(0, 1, smoothstep(-w, w, spec - threshold));
```

**实现**

```c++
Shader "Unity Shaders Book/Chapter 14/Toon Shading"{
	Properties{
		_Color("Color Tint", Color) = (1, 1, 1, 1)
		_MainTex("Main Tex", 2D) = "white" {}
		// 控制漫反射色调的渐变纹理
		_Ramp("Ranmp Texture", 2D) = "white" {}
		// 控制轮廓线宽度
		_Outline("Outline", Range(0, 1)) = 0.1 
		// 轮廓线颜色
		_OutlineColor("Outline Color", Color) = (0, 0, 0, 1)
		_Specular("Specular", Color) = (1, 1, 1, 1)
		// 高光反射时的阈值
		_SpecularScale("Specular Scale", Range(0, 0.1)) = 0.01
	}

	SubShader{
		Pass{
			// 非真实渲染中描边是非常常见的效果，为该Pass定义名称可以在后面直接引用
			NAME "OUTLINE"

			// 渲染背面
			Cull Front

			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag

			fixed _Outline;
			fixed4 _OutlineColor;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};

			struct v2f{
				float4 pos : SV_POSITION;
			};

			v2f vert(a2v v){
				v2f o;

				// 将顶点和法线转到视角空间下
				float4 pos = mul(UNITY_MATRIX_MV, v.vertex);
				float3 normal = mul((float3x3)UNITY_MATRIX_IT_MV, v.normal);
				// 设置法线z分量避免背面扩张后的顶点挡住正面的面片，然后对其归一化再将顶点沿其方向扩张
				normal.z = -0.5;
				pos = pos + float4(normalize(normal), 0) * _Outline;
				o.pos = mul(UNITY_MATRIX_P, pos);

				return o;
			}

			float4 frag(v2f i) : SV_Target{
				// 使用轮廓线颜色渲染整个背面
				return float4(_OutlineColor.rgb, 1);
			}

			ENDCG
		}

		Pass{
			Tags { "LightMode" = "ForwardBase" }

			Cull Back 

			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag

			#pragma multi_compile_fwdbase 

			#include "UnityCG.cginc"
			#include "AutoLight.cginc"
			#include "Lighting.cginc"

			fixed4 _Color;
			sampler2D _MainTex;
			float4 _MainTex_ST;
			sampler2D _Ramp;
			fixed4 _Specular;
			fixed _SpecularScale;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float2 texcoord : TEXCOORD0;
			};

			struct v2f{
				float4 pos : POSITION;
				float2 uv : TEXCOORD0;
				float3 worldNormal : TEXCOORD1;
				float3 worldPos : TEXCOORD2;
				SHADOW_COORDS(3)
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);
				o.worldNormal = mul(v.normal, (float3x3)unity_WorldToObject);
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

				TRANSFER_SHADOW(o);
				
				return o;
			}

			float4 frag(v2f i) : SV_Target{
				fixed3 worldNormal = normalize(i.worldNormal);
				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
				fixed3 worldViewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
				fixed3 worldHalfDir = normalize(worldLightDir + worldViewDir);

				fixed4 color = tex2D(_MainTex, i.uv);
				fixed3 albedo = color.rgb * _Color.rgb;

				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.rgb * albedo;

				UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);

				fixed diff = dot(worldNormal, worldLightDir);
				diff = (diff * 0.5 + 0.5) * atten;

				fixed3 diffuse = _LightColor0.rgb * albedo * tex2D(_Ramp, float2(diff, diff)).rgb;

				fixed spec = dot(worldNormal, worldHalfDir);
				// fwidth可以获取领域像素之间的近似导数值。一次进行高光边缘的抗锯齿处理
				fixed w = fwidth(spec) * 2.0;
				// *step(...)为了再_SpecularScale为0时完全消除高光反射
				fixed3 specular = _Specular.rgb * lerp(0, 1, smoothstep(-w, w, spec * _SpecularScale - 1)) * step(0.0001, _SpecularScale);

				return fixed4(ambient + diffuse + specular, 1.0);
			}

			ENDCG
		}
	}
	FallBack "Specular"
}
```

### 14.2 素描风格的渲染

- 微软研究院的Praum使用了提前生成的素描纹理来实现实时的素描风格纹理渲染，这些纹理组成了一个色调艺术映射（TAM）。从左到右纹理中的笔触逐渐增多用于模拟不同光照下的漫反射效果，从上到下对应了每张纹理的多级渐远纹理，其不是简单的对上一层纹理进行降采样，而需要保持笔触之间的间隔，用于更真实的模拟素描效果

```c++
Shader "Unity Shaders Book/Chapter 14/Hatching"{
	Properties{
		_Color("Color Tint", Color) = (1, 1, 1, 1)
		// 纹理平铺系数，越大模型上的素描线条越密
		_TileFactor("Tile Factor", Float) = 1
		_Outline("Outline", Range(0, 1)) = 0.1
		_Hatch0("Hatch 0", 2D) = "white" {}
		_Hatch1("Hatch 1", 2D) = "white" {}
		_Hatch2("Hatch 2", 2D) = "white" {}
		_Hatch3("Hatch 3", 2D) = "white" {}
		_Hatch4("Hatch 4", 2D) = "white" {}
		_Hatch5("Hatch 5", 2D) = "white" {}
	}

	SubShader{
		Tags { "RenderType" = "Opaque" "Queue" = "Geometry"}

		UsePass "Unity Shaders Book/Chapter 14/Toon Shading/OUTLINE"

		Pass{
			Tags {"LightMode" = "ForwardBase"}

			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag 

			#pragma multi_compile_fwdbase

			#include "Lighting.cginc"
			#include "UnityCG.cginc"
			#include "AutoLight.cginc"

			fixed4 _Color;
			float _TileFactor;
			fixed _Outline;
			sampler2D _Hatch0;
			sampler2D _Hatch1;
			sampler2D _Hatch2;
			sampler2D _Hatch3;
			sampler2D _Hatch4;
			sampler2D _Hatch5;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float2 texcoord : TEXCOORD0;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float2 uv : TEXCOORD0;
				fixed3 hatchWeights0 : TEXCOORD1;
				fixed3 hatchWeights1 : TEXCOORD2;
				float3 worldPos : TEXCOORD3;
				SHADOW_COORDS(4)
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.uv = v.texcoord.xy * _TileFactor;

				// 获取漫反射系数
				fixed3 worldLightDir = normalize(WorldSpaceLightDir(v.vertex));
				fixed3 worldNormal = UnityObjectToWorldNormal(v.normal);
				fixed diff = max(0, dot(worldLightDir, worldNormal));

				o.hatchWeights0 = fixed3(0, 0, 0);
				o.hatchWeights1 = fixed3(0, 0, 0);

				// 将漫反射扩大到[0, 7]范围
				float hatchFactor = diff * 7.0;

				// 判断其所处的子区间范围计算对应的纹理混合权重
				if(hatchFactor > 6.0){

				}else if(hatchFactor > 5.0){
					o.hatchWeights0.x = hatchFactor - 5.0;
				}else if(hatchFactor > 4.0){
					o.hatchWeights0.x = hatchFactor - 4.0;
					o.hatchWeights0.y = 1.0 - o.hatchWeights0.x;
				}else if(hatchFactor > 3.0){
					o.hatchWeights0.y = hatchFactor - 3.0;
					o.hatchWeights0.z = 1.0 - o.hatchWeights0.y;
				}else if(hatchFactor > 2.0){
					o.hatchWeights0.z = hatchFactor - 2.0;
					o.hatchWeights1.x = 1.0 - o.hatchWeights0.z;
				}else if(hatchFactor > 1.0){
					o.hatchWeights1.x = hatchFactor - 1.0;
					o.hatchWeights1.y = 1.0 - o.hatchWeights1.x;
				}else{
					o.hatchWeights1.y = hatchFactor;
					o.hatchWeights1.z = 1.0 - o.hatchWeights1.y;
				}

				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

				TRANSFER_SHADOW(o);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				// 对六个纹理进行采样并乘以对应权重
				fixed4 hatchTex0 = tex2D(_Hatch0, i.uv) * i.hatchWeights0.x;
				fixed4 hatchTex1 = tex2D(_Hatch1, i.uv) * i.hatchWeights0.y;
				fixed4 hatchTex2 = tex2D(_Hatch2, i.uv) * i.hatchWeights0.z;
				fixed4 hatchTex3 = tex2D(_Hatch3, i.uv) * i.hatchWeights1.x;
				fixed4 hatchTex4 = tex2D(_Hatch4, i.uv) * i.hatchWeights1.y;
				fixed4 hatchTex5 = tex2D(_Hatch5, i.uv) * i.hatchWeights1.z;
				// 素描中要有留白部分，希望在最后的渲染中光照最亮的部分是纯白色
				fixed4 whiteColor = fixed4(1, 1, 1, 1) * (1 - i.hatchWeights0.x - i.hatchWeights0.y - i.hatchWeights0.z - i.hatchWeights1.x - i.hatchWeights1.y - i.hatchWeights1.z);

				fixed4 hatchColor = hatchTex0 + hatchTex1 + hatchTex2 + hatchTex3 + hatchTex4 + hatchTex5 + whiteColor;

				UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);

				return fixed4(hatchColor.rgb * _Color.rgb * atten, 1.0);
			}

			ENDCG
		}
	}
	FallBack "Diffuse"
}
```

