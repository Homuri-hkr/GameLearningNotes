### 15.1 消融效果

- 消融常见于游戏中角色的死亡、地图烧毁等，其往往从不同区域开始并向看似随机的方向扩张，最后整个物体都将消失不见
- 实现原理：噪声纹理+透明度测试。对噪声纹理采样的结果和某个控制消融程度的阈值比较，若小于阈值就是用clip将对应的像素裁剪掉，对应了烧毁区域。镂空区域边缘的烧焦效果就是将两种颜色混合，再使用pow函数处理后与原纹理颜色混合后的结果

```c++
Shader "Unity Shaders Book/Chapter 15/Dissolve"{
	Properties{
		// 消融程度
		_BurnAmount("Burn Amount", Range(0.0, 1.0 )) = 0.0
		// 模拟烧焦效果的线宽
		_LineWidth("Burn Line Width", Range(0.0, 0.2)) = 0.1
		// 物体的漫反射和法线纹理
		_MainTex("Base (RGB)", 2D) = "white" {}
		_BumpMap("Normal Map", 2D) = "bump" {}
		// 火焰边缘的两种颜色值
		_BurnFirstColor("Burn First Color", Color) = (1, 0, 0, 1)
		_BurnSecondColor("Burn Second Color", Color) = (1, 0, 0, 1)
		// 噪声纹理
		_BurnMap("Burn Map", 2D) = "white" {}
	}

	SubShader{
		Tags { "RenderType"="Opaque" "Queue"="Geometry"}

		Pass{
			Tags { "LightMode" = "ForwardBase" }

			CUll Off 

			CGPROGRAM

			#include "Lighting.cginc"
			#include "AutoLight.cginc"

			#pragma multi_compile_fwdbase

			#pragma vertex vert 
			#pragma fragment frag 

			fixed _BurnAmount;
			fixed _LineWidth;
			sampler2D _MainTex;
			fixed4 _MainTex_ST;
			sampler2D _BumpMap;
			fixed4 _BumpMap_ST;
			fixed4 _BurnFirstColor;
			fixed4 _BurnSecondColor;
			sampler2D _BurnMap;
			fixed4 _BurnMap_ST;


			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float2 texcoord : TEXCOORD0;
				float4 tangent : TANGENT;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float2 uvMainTex : TEXCOORD0;
				float2 uvBumpMap : TEXCOORD1;
				float2 uvBurnMap : TEXCOORD2;
				float3 lightDir : TEXCOORD3;
				float3 worldPos : TEXCOORD4;
				SHADOW_COORDS(5)
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);
				o.uvMainTex = TRANSFORM_TEX(v.texcoord, _MainTex);
				o.uvBumpMap = TRANSFORM_TEX(v.texcoord, _BumpMap);
				o.uvBurnMap = TRANSFORM_TEX(v.texcoord, _BurnMap);

				// 将光源从模型空间变换到了切线空间
				TANGENT_SPACE_ROTATION;
				o.lightDir = normalize(mul(rotation, ObjSpaceLightDir(v.vertex)).xyz);
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

				TRANSFER_SHADOW(o);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				fixed3 burn = tex2D(_BurnMap, i.uvBumpMap).rgb;
				// 剔除小于消融程度的像素
				clip(burn.r - _BurnAmount);

				float3 tangentLightDir = normalize(i.lightDir);
				fixed3 tangentNormal = UnpackNormal(tex2D(_BumpMap, i.uvBumpMap));

				fixed3 albedo = tex2D(_MainTex, i.uvMainTex).rgb;

				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;

				fixed3 diffuse = _LightColor0.rgb * albedo * max(0, dot(tangentNormal, tangentLightDir));

				// t为0是物体原本颜色，为1是烧融的边界，之间是火焰效果
				fixed t = 1 - smoothstep(0.0, _LineWidth, burn.r - _BurnAmount);
				fixed3 burnColor = lerp(_BurnFirstColor, _BurnSecondColor, t);
				// 让效果更接近烧焦痕迹
				burnColor = pow(burnColor, 5);

				UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
				// 使用step保证_BurnAmount为0时不显示任何消融效果
				fixed3 finalColor = lerp(ambient + diffuse * atten, burnColor, t * step(0.0001, _BurnAmount));

				return fixed4(finalColor, 1);
			}

			ENDCG
		}

		Pass{
			Tags {"LightMode" = "ShadowCaster"}

			CGPROGRAM
			
			#pragma vertex vert 
			#pragma fragment frag 

			#pragma multi_compile_shadowcaster

			#include "UnityCG.cginc"

			fixed _BurnAmount;
			sampler2D _BurnMap;
			float4 _BurnMap_ST;

			struct v2f{
				// 定义阴影投射需要定义的变量
				V2F_SHADOW_CASTER;
				float2 uvBurnMap : TEXCOORD1;
			};

			v2f vert(appdata_base v){
				v2f o;
				// 填充V2F_SHADOW_CASTER中的变量
				TRANSFER_SHADOW_CASTER_NORMALOFFSET(o)

				o.uvBurnMap = TRANSFORM_TEX(v.texcoord, _BurnMap);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				// 剔除片元以显示正确结果
				fixed3 burn = tex2D(_BurnMap, i.uvBurnMap).rgb;
				clip(burn.r - _BurnAmount);
				// 进行阴影投射
				SHADOW_CASTER_FRAGMENT(i);
			}

			ENDCG
		}
	
	}
	Fallback Off
}
```

### 15.2 水波效果

```c++
Shader "Unity Shaders Book/Chapter 15/Water Wave"{
	Properties{
		// 水面颜色
		_Color("Color Tint", Color) = (0, 0.15, 0.115, 1)
		// 水面波纹纹理
		_MainTex("Base (RGB)", 2D) = "white" {}
		// 噪声生成的法线纹理
		_WaveMap("Wave Map", 2D) = "bump" {}
		_Cubemap("Enviroment Cubemap", Cube) = "_Skybox" {}
		// 用于控制法线纹理在X和Y方向的平移
		_WaveXSpeed("Wave Horizeontal Speed", Range(-0.1, 0.1)) = 0.01
		_WaveYSpeed("Wave Vertical Speed", Range(-0.1, 0.1)) = 0.01
		// 折射时图像的扭曲程度
		_Distortion("Distortion", Range(0, 100)) = 10 
	}

	SubShader{
		Tags { "Queue" = "Transparent" "RenderType" = "Opaque"}

		GrabPass {"_RefractionTex"}

		Pass{
			
			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag 

			#include "UnityCG.cginc"

			fixed4 _Color;
			sampler2D _MainTex;
			float4 _MainTex_ST;
			sampler2D _WaveMap;
			float4 _WaveMap_ST;
			samplerCUBE _Cubemap;
			fixed _WaveXSpeed;
			fixed _WaveYSpeed;
			float _Distortion;
			sampler2D _RefractionTex;
			float4 _RefractionTex_TexelSize;

			struct a2v{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float4 tangent : TANGENT;
				float2 texcoord : TEXCOORD0;
			};

			struct v2f{
				float4 pos : SV_POSITION;
				float4 scrPos : TEXCOORD0;
				float4 uv : TEXCOORD1;
				float4 TtoW0 : TEXCOORD2;
				float4 TtoW1 : TEXCOORD3;
				float4 TtoW2 : TEXCOORD4;
			};

			v2f vert(a2v v){
				v2f o;

				o.pos = UnityObjectToClipPos(v.vertex);

				// 获取被抓去屏幕图像的采样坐标
				o.scrPos = ComputeGrabScreenPos(o.pos);

				o.uv.xy = TRANSFORM_TEX(v.texcoord, _MainTex);
				o.uv.zw = TRANSFORM_TEX(v.texcoord, _WaveMap);

				float3 worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
				fixed3 worldNormal = UnityObjectToWorldNormal(v.normal);
				fixed3 worldTangent = UnityObjectToWorldDir(v.tangent.xyz);
				fixed3 worldBinormal = cross(worldNormal, worldTangent) * v.tangent.w;

				o.TtoW0 = float4(worldTangent.x, worldBinormal.x, worldNormal.x, worldPos.x);
				o.TtoW1 = float4(worldTangent.y, worldBinormal.y, worldNormal.y, worldPos.y);
				o.TtoW2 = float4(worldTangent.z, worldBinormal.z, worldNormal.z, worldPos.z);

				return o;
			}

			fixed4 frag(v2f i) : SV_Target{
				float3 worldPos = float3(i.TtoW0.w, i.TtoW1.w, i.TtoW2.w);
				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(worldPos));
				float2 speed = _Time.y * float2(_WaveXSpeed, _WaveYSpeed);

				// 采样两次，模拟两层交叉的水面波动效果
				fixed3 bump1 = UnpackNormal(tex2D(_WaveMap, i.uv.zw + speed)).rgb;
				fixed3 bump2 = UnpackNormal(tex2D(_WaveMap, i.uv.zw - speed)).rgb;
				fixed3 bump = normalize(bump1 + bump2);

				float2 offset = bump.xy * _Distortion * _RefractionTex_TexelSize.xy;
				// 乘以z分量为了模拟深度越大折射程度越大
				i.scrPos.xy = offset * i.scrPos.z + i.scrPos.xy;
				fixed3 refrCol = tex2D(_RefractionTex, i.scrPos.xy / i.scrPos.w).rgb;

				bump = normalize(half3(dot(i.TtoW0.xyz, bump), dot(i.TtoW1.xyz, bump), dot(i.TtoW2.xyz, bump)));
				fixed4 texColor = tex2D(_MainTex, i.uv.xy + speed);
				fixed3 reflDir = reflect(-viewDir, bump);
				fixed3 reflCol = texCUBE(_Cubemap, reflDir).rgb * texColor.rgb * _Color.rgb;
				// 菲涅尔系数
				fixed fresnel = pow(1 - saturate(dot(viewDir, bump)), 4);
				fixed3 color = reflCol * fresnel + refrCol * (1 - fresnel);

				return fixed4(color, 1.0);
			}
			ENDCG
		}

	}
	FallBack Off
}
```

### 15.3 再谈全局雾效

```c#
using UnityEngine;

public class FogWithNoise : PostEffectsBase
{
    public Shader fogShader;
    private Material fogMat;
    public Material material
    {
        get
        {
            fogMat = CheckShaderAndCreateMaterial(fogShader, fogMat);
            return fogMat;
        }
    }

    private Camera mCamera;
    public Camera Camera
    {
        get
        {
            if(mCamera == null)
            {
                mCamera = GetComponent<Camera>();
            }
            return mCamera;
        }
    }

    // 雾的浓度
    [Range(0.1f, 3.0f)]
    public float fogDensity = 1.0f;
    public Color fogColor = Color.white;
    public float fogStart = 0.0f;
    public float fogEnd = 2.0f;
    public Texture noiseTexture;
    [Range(-0.5f, 0.5f)]
    public float fogXSpeed = 0.1f;
    [Range(-0.5f, 0.5f)]
    public float fogYSpeed = 0.1f;
    [Range(0.0f, 3.0f)]
    public float noiseAmount = 1.0f;

    private void OnEnable()
    {
        Camera.depthTextureMode |= DepthTextureMode.Depth;
    }

    private void OnRenderImage(RenderTexture source, RenderTexture destination)
    {
        if(material != null)
        {
            Matrix4x4 frustumCorners = Matrix4x4.identity;

            float fov = Camera.fieldOfView;
            float near = Camera.nearClipPlane;
            float aspect = Camera.aspect;

            // 计算摄像机指向近平面四个角的向量
            float halfHeight = near * Mathf.Tan(fov / 2 * Mathf.Deg2Rad);
            Vector3 toRight = Camera.transform.right * halfHeight * aspect;
            Vector3 toUp = Camera.transform.up * halfHeight;

            Vector3 topLeft = Camera.transform.forward * near + toUp - toRight;
            float scale = topLeft.magnitude / near;

            topLeft.Normalize();
            topLeft *= scale;

            Vector3 topRight = Camera.transform.forward * near + toUp + toRight;
            topRight.Normalize();
            topRight *= scale;

            Vector3 bottomLeft = Camera.transform.forward * near - toUp - toRight;
            bottomLeft.Normalize();
            bottomLeft *= scale;

            Vector3 bottomRight = Camera.transform.forward * near - toUp + toRight;
            bottomRight.Normalize();
            bottomRight *= scale;

            frustumCorners.SetRow(0, bottomLeft);
            frustumCorners.SetRow(1, bottomRight);
            frustumCorners.SetRow(2, topRight);
            frustumCorners.SetRow(3, topLeft);

            material.SetMatrix("_FrustumCornersRay", frustumCorners);
            material.SetFloat("_FogDensity", fogDensity);
            material.SetColor("_FogColor", fogColor);
            material.SetFloat("_FogStargt", fogStart);
            material.SetFloat("_FogEnd", fogEnd);
            material.SetTexture("_NoiseTex", noiseTexture);
            material.SetFloat("_FogXSpeed", fogXSpeed);
            material.SetFloat("_FogYSpeed", fogYSpeed);
            material.SetFloat("_NoiseAmount", noiseAmount);

            Graphics.Blit(source, destination, material);
        }
        else
        {
            Graphics.Blit(source, destination);
        }
    }
}
```

```c++
Shader "Unity Shaders Book/Chapter 15/Water Wave"{
	Properties{
		_MainTex("Base (RGB)", 2D) = "white" {}
		_FogDensity("Fog Density", Float) = 1.0
		_FogColor("Fog Color", Color) = (1, 1, 1, 1)
		_FogStart("Fog Start", Float) = 0.0
		_FogEnd("Fog End", Float) = 1.0 
		_NoiseTex("Noise Texture", 2D) = "white" {}
		_FogXSpeed("Fog Horizzontal Speed", Float) = 0.1
		_FogYSpeed("Fog Vertical Speed", Float) = 0.1 
		_NoiseAmount("Noise Amount", Float) = 1
	}

	SubShader{

		CGINCLUDE
		#include "UnityCG.cginc"

		float4x4 _FrustumCornersRay;
		sampler2D _MainTex;
		half4 _MainTex_TexelSize;
		sampler2D _CameraDepthTexture;
		half _FogDensity;
		fixed4 _FogColor;
		float _FogStart;
		float _FogEnd;
		sampler2D _NoiseTex;
		half _FogXSpeed;
		half _FogYSpeed;
		half _NoiseAmount;

		struct v2f{
			float4 pos : SV_POSITION;
			half2 uv : TEXCOORD0;
			half2 uv_depth : TEXCOORD1;
			float4 interpolatedRay : TEXCOORD2;
		};

		v2f vert(appdata_img v){
			v2f o;

			o.pos = UnityObjectToClipPos(v.vertex);
			o.uv = v.texcoord;
			o.uv_depth = v.texcoord;

			#if UNITY_UV_STARTS_AT_TOP
			if(_MainTex_TexelSize.y < 0)
				o.uv_depth.y = 1 - o.uv_depth.y;
			#endif

			int index = 0;
			if(v.texcoord.x < 0.5 && v.texcoord.y < 0.5){
				index = 0;
			}else if(v.texcoord.x > 0.5 && v.texcoord.y < 0.5){
				index = 1;
			}else if(v.texcoord.x > 0.5 && v.texcoord.y > 0.5){
				index = 2;
			}else{
				index = 3;
			}

			#if UNITY_UV_STARTS_AT_TOP
			if(_MainTex_TexelSize.y < 0)
				index = 3 - index;
			#endif

			o.interpolatedRay = _FrustumCornersRay[index];

			return o;
		}

		fixed4 frag(v2f i) : SV_Target{
			float linearDepth = LinearEyeDepth(SAMPLE_DEPTH_TEXTURE(_CameraDepthTexture, i.uv_depth));
			float3 worldPos = _WorldSpaceCameraPos + linearDepth * i.interpolatedRay.xyz;

			float2 speed = _Time.y * float2(_FogXSpeed, _FogYSpeed);
			float noise = (tex2D(_NoiseTex, i.uv + speed).r - 0.5) * _NoiseAmount;

			float fogDensity = (_FogEnd - worldPos.y) / (_FogEnd - _FogStart);
			fogDensity = saturate(fogDensity * _FogDensity * (1 + noise));

			fixed4 color = tex2D(_MainTex, i.uv);
			color.rgb = lerp(color.rgb, _FogColor.rgb, fogDensity);

			return color;
		}

		ENDCG

		Pass{
			CGPROGRAM

			#pragma vertex vert 
			#pragma fragment frag 

			ENDCG
		}
	}
	FallBack Off
}
```

